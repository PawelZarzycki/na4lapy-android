apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "pl.kodujdlapolski.na4lapy"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 5
        versionName "1.0.1"
        manifestPlaceholders = [HOCKEYAPP_APP_ID: "1b336de533e0489b894c1b202a264a70"]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            res.srcDirs =
                    ['src/main/res/layouts/about_shelter',
                     'src/main/res/layouts/browse',
                     'src/main/res/layouts/details',
                     'src/main/res/layouts/drawer',
                     'src/main/res/layouts/preferences',
                     'src/main/res/layouts',
                     'src/main/res/layouts/settings',
                     'src/main/res/layouts/splash',
                     'src/main/res/layouts/introduction',
                     'src/main/res'
                    ]
        }
    }

    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
        }
        prod {
            buildConfigField 'String', 'BASE_URL', '"http://na4lapy.kodujdlapolski.pl/api/"'
        }
    }

    signingConfigs {
        def keyPass = project.hasProperty('keyPassword') ? keyPassword.toString() : 'pass'
        def storePass = project.hasProperty('storePassword') ? storePassword.toString() : 'pass'

        playstore {
            keyAlias 'na4lapy'
            keyPassword keyPass
            storeFile file('../keystore/na4lapy.keystore')
            storePassword storePass
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.playstore
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor' // butterknife
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'

        testOptions {
            unitTests.returnDefaultValues = true
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    def supportLibraryVersion = "23.4.0"
    def retrofitVersion = "2.0.2"
    dependencies {
        compile fileTree(dir: 'libs', include: ['*.jar'])
        compile "com.android.support:appcompat-v7:$supportLibraryVersion"
        compile "com.android.support:design:$supportLibraryVersion"
        compile "com.android.support:support-annotations:$supportLibraryVersion"
        compile "com.android.support:support-v4:$supportLibraryVersion"
        compile 'com.google.guava:guava:19.0'
        compile 'com.google.dagger:dagger:2.4'
        apt 'com.google.dagger:dagger-compiler:2.4'
        compile 'com.jakewharton:butterknife:8.0.1'
        apt 'com.jakewharton:butterknife-compiler:8.0.1'
        compile 'com.j256.ormlite:ormlite-android:4.48'
        compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
        compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
        compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"
        compile 'com.squareup.picasso:picasso:2.5.2'
        compile 'io.reactivex:rxandroid:1.1.0'
        compile 'io.reactivex:rxjava:1.1.2'
        compile 'jp.wasabeef:picasso-transformations:2.1.0'
        compile 'net.hockeyapp.android:HockeySDK:3.7.2'
        compile 'joda-time:joda-time:2.9.3'
        provided 'org.projectlombok:lombok:1.12.6'
        apt "org.projectlombok:lombok:1.12.6"

        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-core:1.10.19'
        testCompile "org.powermock:powermock-module-junit4:1.6.2"
        testCompile "org.powermock:powermock-api-mockito:1.6.2"

        androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
        androidTestCompile 'com.android.support.test:runner:0.4.1'
        androidTestCompile "com.android.support:support-annotations:$supportLibraryVersion"
        androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2'
        androidTestCompile 'com.android.support.test.espresso:espresso-web:2.2.1'
    }
}